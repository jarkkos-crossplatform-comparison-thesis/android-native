apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply plugin: "androidx.navigation.safeargs"

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        applicationId "fi.jarkkos.thesisbenchmarking.androidnative"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.versionCode
        versionName versions.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }


    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }


    buildTypes {
        release {
            signingConfig signingConfigs.release

            minifyEnabled true
            useProguard true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            resValue "string", "app_version_name", "version ${versions.versionName}"
        }

        debug {
            resValue "string", "app_version_name", "version ${versions.versionName} DEBUG"
        }
    }


}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation deps.kotlinStdLib

    testImplementation deps.junit

    androidTestImplementation deps.androidTestRunner
    androidTestImplementation deps.espressoCore

    implementation deps.androidAppcompat
    implementation deps.constraintLayout
    implementation deps.materialDesign

    implementation deps.coroutinesCore
    implementation deps.coroutinesAndroid

    implementation deps.jetpackNavigationFragment
    implementation deps.jetpackNavigationUi
}
